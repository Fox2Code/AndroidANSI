plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

final int androidTargetSdk = // Parse property once
        Integer.parseInt(project['andorid.targetSdk'] as String)

group = 'com.github.Fox2Code.AndoridANSI'
version = project['library.version'] as String

android {
    namespace 'com.fox2code.androidansi.ktx'
    compileSdk androidTargetSdk

    defaultConfig {
        minSdk 16
        targetSdk androidTargetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api(project(":library"))
    compileOnly('androidx.appcompat:appcompat:1.6.1')
    compileOnly('androidx.compose.ui:ui-text:1.4.3')
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = project.group
                artifactId = 'library-ktx'
                version = '1.0' // JitPack only support 1.0
            }
        }
    }
}